Option Explicit

Sub GeneratePasswords()

    Randomize Timer

    ' 使用する文字セット（小文字・大文字・数字・記号）を定義するための変数
    Dim lowercase As String
    Dim uppercase As String
    Dim digits As String
    Dim symbols As String
    Dim allChars As String
    
    ' パスワードの長さ・生成する個数
    Dim passwordLength As Integer
    Dim passwordCount As Integer
    
    ' ループ制御や作業用の変数
    Dim i As Long, j As Long
    Dim result As String
    Dim password As String
    Dim usedChars As String
    Dim charIndex As Long

    ' 処理対象のシートを指定（「パスワード生成」シートを使用）
    Dim ws As Worksheet
    Set ws = Worksheets("パスワード生成")

    ' 各種文字セットを初期化
    lowercase = "abcdefghijklmnopqrstuvwxyz"
    uppercase = "ABCDEFGHIJKLMNOPQRSTUVWXYZ"
    digits = "0123456789"
    symbols = "@-_!"

    ' 「紛らわしい文字を除去」チェックボックスがオンの場合、i, l, o, I, O, 0, 1 を削除
    If ws.OLEObjects("chkExcludeSimilar").Object.Value = True Then
        lowercase = Replace(lowercase, "i", "")
        lowercase = Replace(lowercase, "l", "")
        lowercase = Replace(lowercase, "o", "")
        uppercase = Replace(uppercase, "I", "")
        uppercase = Replace(uppercase, "O", "")
        digits = Replace(digits, "0", "")
        digits = Replace(digits, "1", "")
    End If

    ' 選択された文字種を「allChars」にまとめる
    allChars = ""
    If ws.OLEObjects("chkLower").Object.Value = True Then allChars = allChars & lowercase
    If ws.OLEObjects("chkUpper").Object.Value = True Then allChars = allChars & uppercase
    If ws.OLEObjects("chkDigit").Object.Value = True Then allChars = allChars & digits
    If ws.OLEObjects("chkSymbol").Object.Value = True Then allChars = allChars & symbols

    ' 何も選択されていなければエラー表示
    If Len(allChars) = 0 Then
        MsgBox "使用する文字の種類を1つ以上選択してください", vbExclamation
        Exit Sub
    End If

    ' 入力値を取得（C2：パスワードの長さ、C3：生成個数）
    passwordLength = ws.Range("C2").Value
    passwordCount = ws.Range("C3").Value

    ' 「重複文字なし」設定時に、要求長さが使用可能文字数を超えていればエラー
    If ws.OLEObjects("chkNoDup").Object.Value = True And passwordLength > Len(allChars) Then
        MsgBox "使用文字数が多すぎます（重複なしにできません）", vbCritical
        Exit Sub
    End If

    ' 出力領域（A2:A50）をクリア
    ws.Range("A2:A50").ClearContents

    ' パスワードを生成
    For i = 1 To passwordCount
        password = ""     ' 生成中のパスワード
        usedChars = ""    ' 既に使った文字（重複防止用）
        
        For j = 1 To passwordLength
            Do
                ' ランダムに1文字選択
                charIndex = Int(Rnd() * Len(allChars)) + 1
                result = Mid(allChars, charIndex, 1)
            ' 「重複なし」設定時は既に使った文字はスキップ
            Loop While ws.OLEObjects("chkNoDup").Object.Value = True And InStr(usedChars, result) > 0
            
            ' 選ばれた文字をパスワードに追加
            password = password & result
            usedChars = usedChars & result
        Next j
        
        ' シートのA列に出力（A2から下へ）
        ws.Cells(1 + i, 1).Value = password
    Next i

    ' 完了メッセージ
    MsgBox "Done!", vbInformation

End Sub

